import org.gradle.api.tasks.PathSensitivity

plugins {
	id 'com.github.node-gradle.node' version '3.1.1'
	id 'java'
}

node {
	version = '14.4.0'
	npmVersion = '6.14.4'
	download = true
}

def npmBuild = project.tasks.register('npmRunBuild', NpmTask) {
	args.addAll(['run', 'build'])
	dependsOn 'npmInstall'

	// Add inputs, this enables up-to-date checks
	inputs.dir(project.file('src'))
		.withPathSensitivity(PathSensitivity.RELATIVE)
	inputs.file(project.file('webpack.config.js'))
		.withPropertyName("webpack.config.js")
	inputs.file(project.file('package.json'))
		.withPropertyName("package.json")

	// Add output, this lets us reference the task in the jar from()
	outputs.dir("${project.buildDir}/dist")
		.withPropertyName("dist")
}

jar {
	from npmBuild
	eachFile { details ->
		details.path = details.path.startsWith('META-INF') ?: 'static/'+details.path
	}
	includeEmptyDirs = false
}